buildscript {
  repositories {
    maven {
      url "https://maven.eveoh.nl/content/repositories/releases"
    }
  }

  dependencies {
    classpath "nl.eveoh:gradle-aspectj:1.6"
  }
}

plugins {
  id 'java'
  id 'eclipse'
  id 'maven'
  id 'jacoco'
  // id 'pmd'
}

project.ext {
    aspectjVersion = '1.8.6'
}

apply plugin: 'aspectj'
 


/*
 * Gets the version name from the latest Git tag of the form V-X.Y where X and Y are version numbers
 */
def getVersionName = { ->
  def stdout = new ByteArrayOutputStream()
  exec {
    commandLine 'git', 'describe', '--tags', '--match', 'V-*.*'
    standardOutput = stdout
  }
  def v = stdout.toString().trim() =~ /V-(\d+)\.(\d+)(?:-(\d+)-)?/
  return v[0][1] + "." + v[0][2] + "." + (v[0][3] == null ? 0 : v[0][3])
}


group = 'ch.kerbtier.esdi'
version = getVersionName()


sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
  mavenLocal()
  mavenCentral()
}

configurations {
  exclude
}

dependencies {
  // otherwise aspects won't be weaved into the testclasses from the main code
  testAspectpath sourceSets.main.output 
  testCompile 'org.testng:testng:6.9.6'
}


compileTestAspect {
  additionalAjcArgs = ['X' : 'hasMember', 'showWeaveInfo': 'true', 'debug': 'true']
}

compileAspect {
  additionalAjcArgs = ['X' : 'hasMember', 'showWeaveInfo': 'true', 'debug': 'true']
}
  

test {
  useTestNG()
  
  testLogging {
    exceptionFormat = 'full'
  }
  
  maxHeapSize = '8m'
}

jar {
  baseName = 'esdi'
  version =  version
  manifest {
    attributes 'Implementation-Title': 'esdi', 'Implementation-Version': version
  }
}

jacocoTestReport {
  reports {
    html.enabled true
  }
}

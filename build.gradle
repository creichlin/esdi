import ch.kerbtier.esdi.javassist.InjectEsdi;

buildscript {
  repositories {
    mavenCentral()
    mavenLocal()
  }
  
  dependencies {
    classpath 'org.javassist:javassist:3.20.0-GA'
    classpath 'ch.kerbtier.esdi-injector:esdi-injector:0.1.+'
  }
}

plugins {
  id 'java'
  id 'eclipse'
  id 'maven'
  id 'jacoco'
}

apply from: 'kerbtier-defaults.gradle'


dependencies {
  testCompile 'org.testng:testng:6.9.6'
}

task classInstrumentation(type: EsdiTask) {
  dependsOn compileTestJava
}

class EsdiTask extends DefaultTask {
    @TaskAction
    def greet() {
      def injector = new InjectEsdi();
      
      project.configurations.testCompile.each {
        injector.addClasspath(it)
      }
      
      injector.addClasspath(project.sourceSets.main.output.classesDir)
      injector.addClasspath(project.sourceSets.test.output.classesDir)
      
      project.fileTree(project.sourceSets.test.output.classesDir).each {
        injector.inject(it)
      }
    }
}

test.dependsOn classInstrumentation

test {
  useTestNG()
  
  testLogging {
    exceptionFormat = 'full'
  }
  
  maxHeapSize = '8m'
}

jacocoTestReport {
  reports {
    html.enabled true
  }
}
